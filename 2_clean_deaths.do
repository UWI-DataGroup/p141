** HEADER -----------------------------------------------------
**  DO-FILE METADATA
    //  algorithm name          2_clean_deaths.do
    //  project:                BNR
    //  analysts:               Jacqueline CAMPBELL
    //  date first created      15-JUL-2021
    //  date last modified      15-JUL-2021
    //  algorithm task          Clean death data
    //  status                  Completed
    //  objectve                To have one dataset with cleaned and standardized 2008-2020 death data.
    //  note                    After 2020 Pt.2 was cleaned and imported into 2008-2020 REDCap database, 
    //                          many duplicates were found - also the dataset had changed slightly from when this db was created,
    //                          so decision made to re-clean this dataset.
    //                          To re-build REDCap database with ALL cleaned deaths.

    
    ** General algorithm set-up
    version 16
    clear all
    macro drop _all
    set more off

    ** Initialising the STATA log and allow automatic page scrolling
    capture {
            program drop _all
    	drop _all
    	log close
    	}

    ** Set working directories: this is for DATASET and LOGFILE import and export
    ** DATASETS to encrypted SharePoint folder
    local datapath "X:/The University of the West Indies/DataGroup - repo_data/data_p141"
    ** LOGFILES to unencrypted OneDrive folder (.gitignore set to IGNORE log files on PUSH to GitHub)
    local logpath X:/OneDrive - The University of the West Indies/repo_datagroup/repo_p141

    ** Close any open log file and open a new log file
    capture log close
    log using "`logpath'\2_clean_deaths_2008-2020.smcl", replace
** HEADER -----------------------------------------------------


***************
** PREP DATASET  
***************
** Prep 2020 deaths dataset for inclusion to 2008-2020 dataset
use "`datapath'\version05\1-input\2020_deaths_cleaned_dqi_dc"
sort record_id
gen record_id2=record_id
replace record_id=.
gen natregno2=nrn
destring nrn ,replace //need to do this for the dataset to append

sort record_id2

save "`datapath'\version05\2-working\2020_deaths_append_dp" ,replace
clear


***************
** LOAD DATASET  
***************
** Load 2008-2020 dataset
use "`datapath'\version05\2-working\2008-2020_deaths_prepped_dp"

count //31,163

tab dodyear ,m //213 missing are TF records
//list record_id dod event if dodyear==.
//1,341 = 2020 deaths

** Remove previously cleaned 2020 deaths from 2020 Pt.1 cleaning process
drop if dodyear==2020 //1341 deleted
count //29,822


** Append full 2020 cleaned deaths and tracking records
append using "`datapath'\version05\2-working\2020_deaths_append_dp" ,force
count //32,512


** Check for duplicate TF records and remove using tfdddoa and regnumstart
gen tfdddoa2=tfdddoa
format tfdddoa2 %tdCCYY-NN-DD
tostring tfdddoa2 ,replace

gen tfregnumstart2=tfregnumstart
tostring tfregnumstart2 ,replace

sort tfregnumstart2 tfdddoa2 record_id
quietly by tfregnumstart2 tfdddoa2 : gen duprgdoa = cond(_N==1,0,_n)
sort tfregnumstart2 tfdddoa2 record_id
count if event==2 & duprgdoa>0 //152
//list record_id tfddda tfdddoa tfregnumstart tfdistrictstart record_id2 if event==2 & duprgdoa>0
count if event==2 & duprgdoa>0 & record_id2!=. //46 - these are the ones from the current 2020 deaths dataset
//list record_id tfddda tfdddoa tfregnumstart tfdistrictstart if event==2 & duprgdoa>0 & record_id2!=.
drop if event==2 & duprgdoa>0 & record_id2!=. //46 deleted
drop tfregnumstart2 tfdddoa2 duprgdoa

count //32,466


** Sequentially add record_id to 2020 deaths using last record_id in 2008-2020 dataset
sort record_id record_id2

summarize record_id
display r(max) //31475
display _N //32,466
//checking last record_id and total number of records as an FYI

replace record_id = record_id[_n-1] + 1 if missing(record_id) 



*****************
** DATA CLEANING  
*****************
** CLEAN each variable according to below consistency checks and the quality rules in DeathData REDCap database

************************
**  DEATH CERTIFICATE **
**        FORM        **
************************
sort record_id
** record_id (auto-generated by REDCap): FLAG 1
** (1) missing/duplicate
count if record_id==. //0
duplicates list record_id, nolabel sepby(record_id) //0


** event (auto-generated by REDCap): FLAG 2
** (2) missing
count if event==. //0


** dddoa: Y-M-D H:M, readonly: FLAG 3
** (3) missing
count if event==1 & dddoa==. //0


** ddda: FLAG 4
** (4) missing
count if event==1 & ddda==. //0
count if event==1 & (ddda!=4 & ddda!=13 & ddda!=14 & ddda!=20 & ddda!=25 & ddda!=98) //0 - check in Logging in REDCap db
//list record_id ddda if event==1 & (ddda!=4 & ddda!=13 & ddda!=14 & ddda!=20 & ddda!=25 & ddda!=98)


** odda: FLAG 5
** (5) missing
count if ddda==98 & odda=="" //0
//list record_id event dddoa ddda odda if ddda==98 & odda==""
** (6) invalid
count if ddda!=98 & odda!="" //0
//list record_id event dddoa ddda odda if ddda!=98 & odda!=""
replace odda="" if ddda!=98 & odda!="" //0 changes


** certtype: 1=MEDICAL 2=POST MORTEM 3=CORONER 99=ND, required: FLAG 6
** (7) missing
count if event==1 & certtype==. //0
//list record_id event dddoa ddda certtype if event==1 & certtype==.


** regnum: integer, if missing=9999: FLAG 7
** (8) missing
count if event==1 & regnum==.|event==1 & regnum==0 //0
//list record_id ddda regnum if event==1 & regnum==.|event==1 & regnum==0
** (9) invalid
count if event==1 & regnum>9999 //0
** (9) duplicate
gen dodyear2=dodyear
tostring dodyear2 ,replace

sort regnum district dodyear2
quietly by regnum district dodyear2:  gen dupreg = cond(_N==1,0,_n)
sort regnum district dodyear2
count if event==1 & dupreg>0  //2,105 - not duplicates as same regnum + district are repeatedly used by Reg.Dept. even within the same year
sort regnum district
order record_id pname regnum district dodyear2 dod
//list record_id dddoa ddda odda pname regnum district nrn recstatdc if event==1 & dupreg>0

** Remove duplicates
//drop if record_id== //290 deleted
drop dupreg dodyear2


** district: 1=A 2=B 3=C 4=D 5=E 6=F: FLAG 8
** (10) missing
count if event==1 & district==. //0


** pname: Text, if missing=99: FLAG 9
** (11) missing
count if event==1 & pname=="" //0
** (12) invalid
count if event==1 & regexm(pname, "[a-z]") //0
//list record_id event ddda odda pname if event==1 & regexm(pname, "[a-z]")
replace pname=upper(pname) //0 changes
count if event==1 & regexm(pname,"NIL")|event==1 & regexm(pname,"ND") //2706 - all correct
//list record_id ddda odda pname if event==1 & regexm(pname,"NIL")|event==1 & regexm(pname,"ND")
** (13) duplicate
sort pname
quietly by pname:  gen dup = cond(_N==1,0,_n)
sort pname
count if event==1 & dup>0  //3,190 - many are not duplicates but 2019 had a few which I've removed below
sort pname record_id
order record_id pname regnum district dodyear dod namematch
//list record_id dddoa ddda odda pname regnum district nrn if event==1 & dup>0
replace namematch=1 if event==1 & dup>0 & namematch!=1 //371
drop if record_id==27730 //1 deleted
//drop if event==1 & dup>0 & namematch==. // deleted
drop dup


** address: Text, if missing=99: FLAG 10
** (14) missing
count if event==1 & address=="" //0
replace address="99" if event==1 & address=="" //0 changes
** (15) invalid 
count if event==1 & regexm(address, "[a-z]") //0
//list record_id event ddda odda address if event==1 & regexm(address, "[a-z]")
replace address=upper(address) //0 changes
count if event==1 & regexm(address,"NIL")|event==1 & regexm(address,"ND") //5,604 - all correct
count if event==1 & regexm(address," ND") //7
count if event==1 & regexm(address," ND ") //7
count if event==1 & regexm(address," ND") & !(strmatch(strupper(address), "*2 ND*")) //0
//list record_id ddda odda address if event==1 & regexm(address,"NIL")|event==1 & regexm(address,"ND")
replace address=subinstr(address," ND","",.) if event==1 & regexm(address," ND") & !(strmatch(strupper(address), "*2 ND*")) //143 changes
count if length(address)<4 & address!="99" //266
replace address="99" if address=="69" //1 change
count if length(address)<4 & address!="99" & address!="" //10 - all correct
count if address=="ND" //0
replace address="99" if address=="ND" //0 changes


** parish: FLAG 11
** (16) missing
count if event==1 & parish==. //0


** sex:	1=Male 2=Female 99=ND: FLAG 12
** (17) missing
count if event==1 & sex==. //0
//list record_id event ddda pname sex if event==1 & sex==.
** (18) invalid - female with prostate
count if sex==2 & (regexm(cod1a, "PROSTAT")|regexm(cod1b, "PROSTAT")|regexm(cod1c, "PROSTAT") ///
		|regexm(cod1d, "PROSTAT")|regexm(cod2a, "PROSTAT")|regexm(cod2b, "PROSTAT")) //0
//list record_id ddda pname nrn sex cod* if (regexm(cod1a, "PROSTAT")|regexm(cod1b, "PROSTAT")|regexm(cod1c, "PROSTAT")|regexm(cod1d, "PROSTAT")|regexm(cod2a, "PROSTAT")|regexm(cod2b, "PROSTAT")) & sex==2
//recode sex 2=1 if record_id==1634 //1 change
** (19) visual check - first names for those missing nrn
count if sex==1 & nrn==. //2,603 - check if there is a stata check for this e.g. soundex,etc - 0 changes
//list record_id ddda pname sex if sex==1 & nrn==.
//recode sex 1=2 if record_id== //0 changes
** (20) invalid - male with female genital cod
count if sex==1 & (regexm(cod1a, "UTER") | regexm(cod1a, "OMA OF THE VULVA") | ///
			regexm(cod1a, "CHORIOCARCIN") | regexm(cod1a, "ENDOMETRIAL CARCINOMA") | ///
			regexm(cod1a, "ENDOMETRIAL CANC") | regexm(cod1a, "OF ENDOMETRIUM") | ///
			regexm(cod1a, "OF THE ENDOMETRIUM") | regexm(cod1a, "VULVA CARCINOMA") | ///
			regexm(cod1a, "VULVAL CANCER") | regexm(cod1a, "VAGINAL CARCINOMA")|regexm(cod1a, "ENDOMETRIUM")) //3 - correct
/*list record_id ddda pname nrn sex cod* if sex==1 & (regexm(cod1a, "UTER") | regexm(cod1a, "OMA OF THE VULVA") | ///
			regexm(cod1a, "CHORIOCARCIN") | regexm(cod1a, "ENDOMETRIAL CARCINOMA") | ///
			regexm(cod1a, "ENDOMETRIAL CANC") | regexm(cod1a, "OF ENDOMETRIUM") | ///
			regexm(cod1a, "OF THE ENDOMETRIUM") | regexm(cod1a, "VULVA CARCINOMA") | ///
			regexm(cod1a, "VULVAL CANCER") | regexm(cod1a, "VAGINAL CARCINOMA")|regexm(cod1a, "ENDOMETRIUM"))
*/
//recode sex 1=2 if record_id==70|record_id==711 //2 changes
** (21) visual check - first names for those missing nrn
count if sex==2 & nrn==. //2,509 - check if there is a stata check for this e.g. soundex,etc - 0 changes
//list record_id ddda pname sex if sex==2 & nrn==.


** age: Integer - min=0, max=999: FLAG 13
** (22) missing
count if event==1 & age==. //0
** (23) missing - NRN not missing
count if (age==.|age==0) & nrn!=. //0
//list record_id pname age agetxt nrn dod cod1a if (age==.|age==0) & nrn!=. ,string(70)
/*
replace age=2 if record_id==15056
replace agetxt=5 if record_id==15056
*/

** agetxt - 1 "Minutes" 2 "Hours" 3 "Days" 4 "Weeks" 5 "Months" 6 "Years" 99 "ND": FLAG 14
** (25) missing
count if age!=. & agetxt==. //0
** (26) invalid
count if age==999 & agetxt!=99 //0
count if age!=999 & age!=. & agetxt==99 //0
replace agetxt=6 if age!=999 & age!=. & agetxt==99 //0 changes
** (27) visual check - NRN vs agetxt
count if nrn!=. & age!=. & agetxt!=6 //16
//list record_id ddda odda dod nrn age agetxt if nrn!=. & age!=. & agetxt!=6 //checked these using redcap db & electoral list


** dod: Y-M-D (need to clean dod before other checks): FLAG 15
** (28) missing
count if event==1 & dod==. //0
** (29) invalid - future date
gen currentd=c(current_date)
gen double today=date(currentd, "DMY")
drop currentd
format today %tdCCYY-NN-DD
count if event==1 & dod>today //0
sort record_id
//list record_id ddda dod regdate pname if event==1 & dod>today
** (30) invalid - after reg date
count if event==1 & dod>regdate //3 (check redcapdb for if any coroner cases; 2019 TF redcap report for 'true' 2019 cases)
//list record_id ddda odda certtype dod regdate pname if event==1 & dod>regdate
//swapval regdate dod if dod>regdate //ssc install swapval - doesn't work for dates, only for numeric or string values
gen dod2=regdate if dod>regdate
gen regdate2=dod if dod>regdate
format dod2 regdate2 %tdCCYY-NN-DD
replace dod=dod2 if dod2!=. //3 changes
replace regdate=regdate2 if regdate2!=. //3 changes
drop dod2 regdate2


** dodyear (not included in single year Redcap db but done for multi-year Redcap db): FLAG 16
** (31) missing
count if event==1 & dodyear==. //0
//list record_id ddda dod regdate if event==1 & dodyear==.
** (32) invalid - deaths before/after 2019
count if event==1 & dodyear<2008 & dodyear>2020 //0
//list record_id ddda odda dod dodyear pname nrn regdate if event==1 & dodyear!=2020
//tab dodyear if event==1,m
/*
    Year of |
      Death |      Freq.     Percent        Cum.
------------+-----------------------------------
       2008 |      2,472        7.67        7.67
       2009 |      2,356        7.31       14.99
       2010 |      2,303        7.15       22.14
       2011 |      2,385        7.40       29.54
       2012 |      2,373        7.37       36.91
       2013 |      2,410        7.48       44.39
       2014 |      2,496        7.75       52.14
       2015 |      2,486        7.72       59.86
       2016 |      2,489        7.73       67.59
       2017 |      2,525        7.84       75.43
       2018 |      2,528        7.85       83.28
       2019 |      2,785        8.65       91.92
       2020 |      2,602        8.08      100.00
------------+-----------------------------------
      Total |     32,210      100.00
*/
//drop if event==1 & dodyear>2020 //0 deleted

** nrnnd: 1=Yes 2=No: FLAG 17
** (33) missing
count if event==1 & nrnnd==. //0
//list record_id ddda nrn if event==1 & nrnnd==.
** (34) invalid
count if event==1 & nrn==. & nrnnd==1 //0
//list record_id ddda pname address age agetxt dod nrn nrnnd if event==1 & nrn==. & nrnnd==1


/*
preserve
clear
import excel using "`datapath'\version05\1-input\NRNmissing_elec_list.xlsx" , firstrow case(lower)
save "`datapath'\version05\2-working\2008-2020_deaths_missing_nrn" ,replace
restore
merge 1:1 record_id using "`datapath'\version05\2-working\2008-2020_deaths_missing_nrn" ,force
/*
    Result                           # of obs.
    -----------------------------------------
    not matched                        31,158
        from master                    31,158  (_merge==1)
        from using                          0  (_merge==2)

    matched                                21  (_merge==3)
    -----------------------------------------
*/
destring elec_nrn ,replace
format elec_nrn %15.0g
replace nrn=elec_nrn if nrn==. & elec_nrn!=. //21 changes
replace agetxt=5 if record_id==4711
replace agetxt=5 if record_id==4820
drop elec_* _merge
*/


** (35) invalid
count if nrn!=. & nrnnd==2 //0
//list record_id ddda odda pname nrn nrnnd if nrn!=. & nrnnd==2
//replace nrnnd=1 if nrn!=. & nrnnd==2 //2 changes


** nrn: dob-####, partial missing=dob-9999, if missing=.: FLAG 18
tostring nrn, gen(natregno) format("%10.0f")
replace natregno="" if natregno=="." //2,886 changes
replace natregno=natregno2 if dodyear==2020 //reinserting 2020 NRNs: 18 changes

** (36) missing
count if event==1 & nrnnd==1 & (natregno==""|natregno=="9999999999") //0
replace nrnnd=2 if event==1 & nrnnd==1 & (natregno==""|natregno=="9999999999") //0 changes
** (37) invalid - length (checked against electoral list; no error for DA if leading zero was in redcap db)
count if natregno!="" & length(natregno)!=10 //286
//list record_id ddda odda pname age nrn natregno if natregno!="" & length(natregno)!=10
replace natregno="0" + natregno if length(natregno)==9 //269 changes
replace natregno="00" + natregno if length(natregno)==8 //5 changes
replace natregno="000" + natregno if length(natregno)==7 //12 changes

** (38) invalid - dob but missing nrn #
count if regexm(natregno,"9999")|regexm(natregno,"99") //898 (checked against electoral list)
//list record_id ddda odda natregno pname if regexm(natregno,"9999")|regexm(natregno,"99")
** (39) invalid - female with 'male' NRN
count if sex==2 & (regex(substr(natregno,-2,1), "[1,3,5,7,9]")) & !(strmatch(strupper(natregno), "*-9999*")) //472
//list record_id ddda odda pname natregno sex cod* if sex==2 & (regex(substr(natregno,-2,1), "[1,3,5,7,9]")) & !(strmatch(strupper(natregno), "*-9999*"))
//recode sex 2=1 if record_id==|record_id== //1 change
** (40) invalid - male with 'female' NRN
count if sex==1 & regex(substr(natregno,-2,1), "[0,2,4,6,8]") //77
//list record_id ddda odda pname natregno sex cod* if sex==1 & regex(substr(natregno,-2,1), "[0,2,4,6,8]")
//recode sex 1=2 if record_id==|record_id== //2 changes
** (41) invalid - age vs dob(nrn)
gen dobyr=substr(natregno, 1, 2) if natregno!=""
gen dobmon=substr(natregno, 3, 2) if natregno!=""
gen dobday=substr(natregno, 5, 2) if natregno!=""
count if (agetxt!=6 & natregno!="")|regex(substr(dobyr,1,1),"[0]") //319 - all correct
//list record_id pname age agetxt dod natregno dobyr dobmon dobday if (agetxt!=6 & natregno!="")|regex(substr(dobyr,1,1),"[0]")
replace dobyr="19"+dobyr if (agetxt!=6 & natregno!="")|regex(substr(dobyr,1,1),"[0]") & age>90 //213 changes
replace dobyr="20"+dobyr if age<21 & ((agetxt!=6 & natregno!="")|regex(substr(dobyr,1,1),"[0]")) //124 changes
//replace dobyr="20"+dobyr if (agetxt!=6 & natregno!="")|regex(substr(dobyr,1,1),"[0]") //5 changes
replace dobyr="19"+dobyr if length(dobyr)==2 //29,260 changes

count if length(dobyr)>4 //0
count if dobday!="" & (dobday!="01"&dobday!="02"&dobday!="03"&dobday!="04"&dobday!="05"&dobday!="06"&dobday!="07"&dobday!="08"&dobday!="09"&dobday!="10"&dobday!="11"&dobday!="12" ///
		 &dobday!="13"&dobday!="14"&dobday!="15"&dobday!="16"&dobday!="17"&dobday!="18"&dobday!="19"&dobday!="20"&dobday!="21"&dobday!="22"&dobday!="23"&dobday!="24"&dobday!="25" ///
		 &dobday!="26"&dobday!="27"&dobday!="28"&dobday!="29"&dobday!="30"&dobday!="31") //0
/*list record_id ddda dobday natregno pname if dobday!="" & (dobday!="01"&dobday!="02"&dobday!="03"&dobday!="04"&dobday!="05"&dobday!="06"&dobday!="07"&dobday!="08"&dobday!="09"&dobday!="10"&dobday!="11"&dobday!="12" ///
		 &dobday!="13"&dobday!="14"&dobday!="15"&dobday!="16"&dobday!="17"&dobday!="18"&dobday!="19"&dobday!="20"&dobday!="21"&dobday!="22"&dobday!="23"&dobday!="24"&dobday!="25" ///
		 &dobday!="26"&dobday!="27"&dobday!="28"&dobday!="29"&dobday!="30"&dobday!="31")
*/
//replace dobday="23" if record_id==339 //1 change
//replace natregno=subinstr(natregno,"33","23",.) if record_id==339 //1 change

count if dobmon!="" & (dobmon!="01"&dobmon!="02"&dobmon!="03"&dobmon!="04"&dobmon!="05"&dobmon!="06"&dobmon!="07"&dobmon!="08"&dobmon!="09"&dobmon!="10"&dobmon!="11"&dobmon!="12") //0
//list record_id ddda dobmon natregno pname if dobmon!="" & (dobmon!="01"&dobmon!="02"&dobmon!="03"&dobmon!="04"&dobmon!="05"&dobmon!="06"&dobmon!="07"&dobmon!="08"&dobmon!="09"&dobmon!="10"&dobmon!="11"&dobmon!="12")
//replace dobmon="12" if record_id==186 //1 change
//replace natregno=subinstr(natregno,"13","12",.) if record_id==186 //1 change

gen birthdate2=dobyr+dobmon+dobday
gen dob=date(birthdate2, "YMD")
format dob %tdCCYY-NN-DD
gen age2=int((dod - dob)/365.25) //now use this to assign missing age
count if age!=age2 & natregno!="" & dob!=. //20
sort record_id
//list record_id pname age age2 agetxt dod dob natregno if age!=age2 & natregno!=""

//replace age=95 if record_id==7 //1 change
//replace agetxt=6 if record_id==7 //1 change
//replace age=age2 if age!=age2 & natregno!="" & dob!=. & record_id!=484 //45 changes
replace age=age2 if age!=age2 & natregno!="" & dob!=. & age2!=0 & agetxt==6 & age2!=. & record_id!=4711 & record_id!=24537 & record_id!=24632 & record_id!=24637 & record_id!=25081 & record_id!=25129 & record_id!=27007 & record_id!=27754 & record_id!=28175 & record_id!=30038 & record_id!=32984 & record_id!=33636 & record_id!=33754 & record_id!=33907 & record_id!=33992 & record_id!=33995 //5 changes
count if natregno=="" & natregno2!=""
drop nrn dob dobday dobmon dobyr age2 natregno2
rename natregno nrn


** mstatus: 1=Single 2=Married 3=Separated/Divorced 4=Widowed/Widow/Widower 99=ND: FLAG 19
** (42) missing
count if event==1 & mstatus==. //0


** occu: Text, if missing=99: FLAG 20
** (43) missing
count if event==1 & occu=="" //0
** (44) invalid 
count if event==1 & regexm(occu, "[a-z]") //0
//list record_id ddda odda occu if event==1 & regexm(occu, "[a-z]")
replace occu=upper(occu) //0 changes
replace occu = rtrim(ltrim(itrim(occu))) //3 changes
count if event==1 & regexm(occu,"NIL")|event==1 & regexm(occu,"ND") //1319 - all correct
//list record_id ddda odda occu if event==1 & regexm(occu,"NIL")|event==1 & regexm(occu,"ND")
count if event==1 & occu=="NIL"|event==1 & occu=="ND" //0
//list record_id ddda odda occu if event==1 & occu=="NIL"|event==1 & occu=="ND"
replace occu="99" if event==1 & occu=="NIL"|event==1 & occu=="ND" //0 changes


** durationnum: Integer - min=0, max=99, if missing=99: FLAG 21
** (45) missing
count if event==1 & durationnum==. //0
** (46) invalid
count if durationnum==999 & durationtxt!=. & durationtxt!=99 //29
//list record_id ddda durationnum durationtxt if durationnum==999 & durationtxt!=. & durationtxt!=99
replace durationtxt=99 if durationnum==999 & durationtxt!=. & durationtxt!=99 //29 changes
count if durationnum==99 & durationtxt==99 //0
//list record_id ddda durationnum durationtxt if durationnum==99 & durationtxt==99
replace durationnum=999 if durationnum==99 & durationtxt==99 //0 changes


** durationtxt - 1 "Days" 2 "Weeks" 3 "Months" 4 "Years" 99 "ND": FLAG 22
** (47) missing
count if durationnum!=. & durationnum!=999 & durationtxt==. //24,074
//list record_id ddda durationnum durationtxt if durationnum!=. & durationtxt==.
replace durationnum=999 if durationnum!=. & durationnum!=999 & durationtxt==. //24,074 changes
** (48) possibly invalid
count if durationnum!=999 & durationtxt==99 //6 - correct
//list record_id ddda durationnum durationtxt onset* if durationnum!=999 & durationtxt==99
//replace durationtxt=4 if record_id==351 //1 change
count if durationnum==999 & durationtxt==99 //1
replace durationtxt=. if durationnum==999 & durationtxt==99 //1 change


** cod1a: Text, if missing=99: FLAG 23
** (49) missing
count if event==1 & cod1a=="" //0
** (50) invalid
count if event==1 & regexm(cod1a, "[a-z]") //0
//list record_id ddda cod1a onsetnumcod1a onsettxtcod1a if event==1 & regexm(cod1a, "[a-z]")
replace cod1a=upper(cod1a) //0 changes
replace cod1a = rtrim(ltrim(itrim(cod1a))) //0 changes
count if event==1 & regexm(cod1a,"NIL")|event==1 & regexm(cod1a,"ND") //4,980 - all correct
//list record_id ddda cod1a if event==1 & regexm(cod1a,"NIL")|event==1 & regexm(cod1a,"ND")


** onsetnumcod1a: Integer - min=0, max=99, if missing=99: FLAG 24
** (51) missing
count if event==1 & cod1a!="99" & onsetnumcod1a==. //0
** (52) possibly invalid
count if onsetnumcod1a==999 & onsettxtcod1a!=. & onsettxtcod1a!=99 //134
//list record_id ddda onsetnumcod1a onsettxtcod1a if onsetnumcod1a==999 & onsettxtcod1a!=. & onsettxtcod1a!=99
replace onsetnumcod1a=99 if onsetnumcod1a==999 & onsettxtcod1a!=. & onsettxtcod1a!=99 //134 changes
count if onsetnumcod1a==99 & onsettxtcod1a==99 //0
//list record_id ddda onsetnumcod1a onsettxtcod1a if onsetnumcod1a==99 & onsettxtcod1a==99
replace onsetnumcod1a=999 if onsetnumcod1a==99 & onsettxtcod1a==99 //0 changes


** onsettxtcod1a: 1 "Days" 2 "Weeks" 3 "Months" 4 "Years" 99 "ND": FLAG 25
** (53) missing
count if onsetnumcod1a!=. & onsetnumcod1a!=999 & onsettxtcod1a==. //24,104
//list record_id ddda onsetnumcod1a onsettxtcod1a if onsetnumcod1a!=. & onsetnumcod1a!=999 & onsettxtcod1a==.
replace onsetnumcod1a=999 if onsetnumcod1a!=. & onsetnumcod1a!=999 & onsettxtcod1a==. //24,104 changes
** (54) possibly invalid
count if onsetnumcod1a!=999 & onsettxtcod1a==99 //14 - correct
//list record_id ddda duration* onsetnumcod1a onsettxtcod1a if onsetnumcod1a!=999 & onsettxtcod1a==99
count if onsetnumcod1a==999 & onsettxtcod1a==99 //1
replace onsettxtcod1a=. if onsetnumcod1a==999 & onsettxtcod1a==99 //1 change


** cod1b: Text, if missing=99: FLAG 26
** (55) missing
count if event==1 & cod1b=="" //0
** (56) invalid
count if event==1 & regexm(cod1b, "[a-z]") //0
//list record_id ddda odda cod1b onsetnumcod1b onsettxtcod1b if event==1 & regexm(cod1b, "[a-z]")
replace cod1b=upper(cod1b) //0 changes
replace cod1b = rtrim(ltrim(itrim(cod1b))) //0 changes
count if event==1 & regexm(cod1b,"NIL")|event==1 & regexm(cod1b,"ND") //325 - all correct
//list record_id ddda cod1b if event==1 & regexm(cod1b,"NIL")|event==1 & regexm(cod1b,"ND")


** onsetnumcod1b: Integer - min=0, max=99, if missing=99: FLAG 27
** (57) missing
count if event==1 & cod1b!="99" & onsetnumcod1b==. //0
//list record_id ddda cod1b onsetnumcod1b onsettxtcod1b if event==1 & cod1b!="99" & onsetnumcod1b==.
//replace onsetnumcod1b=999 if event==1 & cod1b!="99" & onsetnumcod1b==. //2 changes
** (58) possibly invalid
count if onsetnumcod1b==999 & onsettxtcod1b!=. & onsettxtcod1b!=99 //110
//list record_id ddda onsetnumcod1b onsettxtcod1b if onsetnumcod1b==999 & onsettxtcod1b!=. & onsettxtcod1b!=99
replace onsetnumcod1b=99 if onsetnumcod1b==999 & onsettxtcod1b!=. & onsettxtcod1b!=99 //0 changes
count if onsetnumcod1b==99 & onsettxtcod1b==99 //0
//list record_id ddda onsetnumcod1b onsettxtcod1b if onsetnumcod1b==99 & onsettxtcod1b==99
//replace onsetnumcod1b=999 if record_id==|record_id== //2 changes


** onsettxtcod1b: 1 "Days" 2 "Weeks" 3 "Months" 4 "Years" 99 "ND": FLAG 28
** (59) missing
count if onsetnumcod1b!=. & onsetnumcod1b!=999 & onsettxtcod1b==. //0
//list record_id ddda onsetnumcod1b onsettxtcod1b if onsetnumcod1b!=. & onsetnumcod1b!=999 & onsettxtcod1b==.
** (60) possibly invalid
count if onsetnumcod1b!=999 & onsettxtcod1b==99 //16
//list record_id ddda duration* onsetnumcod1b onsettxtcod1b if onsetnumcod1b!=999 & onsettxtcod1b==99
replace onsetnumcod1b=999 if onsetnumcod1b==. & onsettxtcod1b==99 //14 changes
//replace onsetnumcod1b=999 if onsetnumcod1b!=999 & onsettxtcod1b==99 //74 changes
count if onsetnumcod1b==999 & onsettxtcod1b==99 //1
replace onsettxtcod1b=. if onsetnumcod1b==999 & onsettxtcod1b==99 //1 change


** cod1c: Text, if missing=99: FLAG 29
** (61) missing
count if event==1 & cod1c=="" //0
** (62) invalid
count if event==1 & regexm(cod1c, "[a-z]") //0
//list record_id ddda cod1c onsetnumcod1c onsettxtcod1c if event==1 & regexm(cod1c, "[a-z]")
replace cod1c=upper(cod1c) //0 changes
replace cod1c = rtrim(ltrim(itrim(cod1c))) //0 changes
count if event==1 & regexm(cod1c,"NIL")|event==1 & regexm(cod1c,"ND") //95 - all correct
//list record_id ddda cod1c if event==1 & regexm(cod1c,"NIL")|event==1 & regexm(cod1c,"ND")


** onsetnumcod1c: Integer - min=0, max=99, if missing=99: FLAG 30
** (63) missing
count if event==1 & cod1c!="99" & onsetnumcod1c==. //0
//list record_id ddda cod1c onsetnumcod1c onsettxtcod1c if event==1 & cod1c!="99" & onsetnumcod1c==.
//replace onsetnumcod1c=999 if event==1 & cod1c!="99" & onsetnumcod1c==. //1 change
** (64) possibly invalid
count if onsetnumcod1c==999 & onsettxtcod1c!=. & onsettxtcod1c!=99 //41
//list record_id ddda onsetnumcod1c onsettxtcod1c if onsetnumcod1c==999 & onsettxtcod1c!=. & onsettxtcod1c!=99
replace onsetnumcod1c=99 if onsetnumcod1c==999 & onsettxtcod1c!=. & onsettxtcod1c!=99 //41 changes
count if onsetnumcod1c==99 & onsettxtcod1c==99 //0
//list record_id ddda onsetnumcod1c onsettxtcod1c if onsetnumcod1c==99 & onsettxtcod1c==99
//replace onsetnumcod1c=999 if record_id==


** onsettxtcod1c: 1 "Days" 2 "Weeks" 3 "Months" 4 "Years" 99 "ND": FLAG 31
** (65) missing
count if onsetnumcod1c!=. & onsetnumcod1c!=999 & onsettxtcod1c==. //0
//list record_id ddda onsetnumcod1c onsettxtcod1c if onsetnumcod1c!=. & onsettxtcod1c==.
** (66) possibly invalid
count if onsetnumcod1c!=999 & onsettxtcod1c==99 //24
//list record_id ddda duration* onsetnumcod1c onsettxtcod1c if onsetnumcod1c!=999 & onsettxtcod1c==99
replace onsetnumcod1c=999 if onsetnumcod1c==. & onsettxtcod1c==99 //22 changes
//replace onsetnumcod1c=999 if onsetnumcod1c!=999 & onsettxtcod1c==99 //131 changes
count if onsetnumcod1c==999 & onsettxtcod1c==99 //1
replace onsettxtcod1c=. if onsetnumcod1c==999 & onsettxtcod1c==99 //1 change


** cod1d: Text, if missing=99: FLAG 32
** (67) missing
count if event==1 & cod1d=="" //0
//list record_id ddda cod1d if event==1 & cod1d==""
** (68) invalid
count if event==1 & regexm(cod1d, "[a-z]") //0
//list record_id ddda cod1d onsetnumcod1d onsettxtcod1d if event==1 & regexm(cod1d, "[a-z]")
replace cod1d=upper(cod1d) //0 changes
replace cod1d = rtrim(ltrim(itrim(cod1d))) //0 changes
count if event==1 & regexm(cod1d,"NIL")|event==1 & regexm(cod1d,"ND") //20 - all correct
//list record_id ddda cod1d if event==1 & regexm(cod1d,"NIL")|event==1 & regexm(cod1d,"ND")


** onsetnumcod1d: Integer - min=0, max=99, if missing=99: FLAG 33
** (69) missing
count if event==1 & cod1d!="99" & onsetnumcod1d==. //0
//list record_id ddda cod1d onsetnumcod1d onsettxtcod1d if event==1 & cod1d!="99" & onsetnumcod1d==.
** (70) possibly invalid
count if onsetnumcod1d==999 & onsettxtcod1d!=. & onsettxtcod1d!=99 //19
//list record_id ddda onsetnumcod1d onsettxtcod1d if onsetnumcod1d==999 & onsettxtcod1d!=. & onsettxtcod1d!=99
replace onsetnumcod1d=99 if onsetnumcod1d==999 & onsettxtcod1d!=. & onsettxtcod1d!=99 //19 changes
count if onsetnumcod1d==99 & onsettxtcod1d==99 //0
//list record_id ddda onsetnumcod1d onsettxtcod1d if onsetnumcod1d==99 & onsettxtcod1d==99


** onsettxtcod1d: 1 "Days" 2 "Weeks" 3 "Months" 4 "Years" 99 "ND": FLAG 34
** (71) missing
count if onsetnumcod1d!=. & onsetnumcod1d!=999 & onsettxtcod1d==. //0
//list record_id ddda onsetnumcod1d onsettxtcod1d if onsetnumcod1d!=. & onsetnumcod1d!=999 & onsettxtcod1d==.
** (72) possibly invalid
count if onsetnumcod1d!=999 & onsettxtcod1d==99 //25
//list record_id ddda duration* onsetnumcod1d onsettxtcod1d if onsetnumcod1d!=999 & onsettxtcod1d==99
replace onsetnumcod1d=999 if onsetnumcod1d!=999 & onsettxtcod1d==99 //25 changes
count if onsetnumcod1d==999 & onsettxtcod1d==99 //1
replace onsettxtcod1d=. if onsetnumcod1d==999 & onsettxtcod1d==99 //1 change


** cod2a: Text, if missing=99: FLAG 35
** (73) missing
count if event==1 & cod2a=="" //0
//list record_id ddda cod2a if event==1 & cod2a==""
** (74) invalid
count if event==1 & regexm(cod2a, "[a-z]") //0
//list record_id ddda cod2a onsetnumcod2a onsettxtcod2a if event==1 & regexm(cod2a, "[a-z]")
replace cod2a=upper(cod2a) //0 changes
replace cod2a = rtrim(ltrim(itrim(cod2a))) //0 changes
count if event==1 & regexm(cod2a,"NIL")|event==1 & regexm(cod2a,"ND") //151 - all correct
//list record_id ddda cod2a if event==1 & regexm(cod2a,"NIL")|event==1 & regexm(cod2a,"ND")


** onsetnumcod2a: Integer - min=0, max=99, if missing=99: FLAG 36
** (75) missing
count if event==1 & cod2a!="99" & onsetnumcod2a==. //0
//list record_id ddda cod2a onsetnumcod2a onsettxtcod2a if event==1 & cod2a!="99" & onsetnumcod2a==.
//replace onsetnumcod2a=999 if event==1 & cod2a!="99" & onsetnumcod2a==. //1 change
** (76) possibly invalid
count if onsetnumcod2a==999 & onsettxtcod2a!=. & onsettxtcod2a!=99 //143
//list record_id ddda onsetnumcod2a onsettxtcod2a if onsetnumcod2a==999 & onsettxtcod2a!=. & onsettxtcod2a!=99
replace onsetnumcod2a=99 if onsetnumcod2a==999 & onsettxtcod2a!=. & onsettxtcod2a!=99 //143 changes
count if onsetnumcod2a==99 & onsettxtcod2a==99 //0
//list record_id ddda onsetnumcod2a onsettxtcod2a if onsetnumcod2a==99 & onsettxtcod2a==99
replace onsetnumcod2a=999 if onsetnumcod2a==99 & onsettxtcod2a==99 //0 changes


** onsettxtcod2a: 1 "Days" 2 "Weeks" 3 "Months" 4 "Years" 99 "ND": FLAG 37
** (77) missing
count if onsetnumcod2a!=. & onsetnumcod2a!=999 & onsettxtcod2a==. //0
//list record_id ddda onsetnumcod2a onsettxtcod2a if onsetnumcod2a!=. & onsetnumcod2a!=999 & onsettxtcod2a==.
//replace onsettxtcod2a=99 if onsetnumcod2a!=. & onsettxtcod2a==. //1 change
** (78) possibly invalid
count if onsetnumcod2a!=999 & onsettxtcod2a==99 //18
//list record_id ddda duration* onsetnumcod2a onsettxtcod2a if onsetnumcod2a!=999 & onsettxtcod2a==99
replace onsetnumcod2a=999 if onsetnumcod2a==. & onsettxtcod2a==99 //15 changes
//replace onsetnumcod2a=999 if onsetnumcod2a!=999 & onsettxtcod2a==99 //84 changes
count if onsetnumcod2a==999 & onsettxtcod2a==99 //1
replace onsettxtcod2a=. if onsetnumcod2a==999 & onsettxtcod2a==99 //1 change


** cod2b: Text, if missing=99: FLAG 38
** (79) missing
count if event==1 & cod2b=="" //0
//list record_id ddda cod2b if event==1 & cod2b==""
** (80) invalid
count if event==1 & regexm(cod2b, "[a-z]") //0
//list record_id ddda cod2b onsetnumcod2b onsettxtcod2b if event==1 & regexm(cod2b, "[a-z]")
replace cod2b=upper(cod2b) //0 changes
replace cod2b = rtrim(ltrim(itrim(cod2b))) //0 changes
count if event==1 & regexm(cod2b,"NIL")|event==1 & regexm(cod2b,"ND") //38 - all correct
//list record_id ddda cod2b if event==1 & regexm(cod2b,"NIL")|event==1 & regexm(cod2b,"ND")


** onsetnumcod2b: Integer - min=0, max=99, if missing=99: FLAG 39
** (81) missing
count if event==1 & cod2b!="99" & onsetnumcod2b==. //0
//list record_id ddda cod2b onsetnumcod2b onsettxtcod2b if event==1 & cod2b!="99" & onsetnumcod2b==.
** (82) possibly invalid
count if onsetnumcod2b==999 & onsettxtcod2b!=. & onsettxtcod2b!=99 //68
//list record_id ddda onsetnumcod2b onsettxtcod2b if onsetnumcod2b==999 & onsettxtcod2b!=. & onsettxtcod2b!=99
replace onsetnumcod2b=99 if onsetnumcod2b==999 & onsettxtcod2b!=. & onsettxtcod2b!=99 //68 changes
count if onsetnumcod2b==99 & onsettxtcod2b==99 //0
//list record_id ddda onsetnumcod2b onsettxtcod2b if onsetnumcod2b==99 & onsettxtcod2b==99
replace onsetnumcod2b=999 if onsetnumcod2b==99 & onsettxtcod2b==99 //0 changes


** onsettxtcod2b: 1 "Days" 2 "Weeks" 3 "Months" 4 "Years" 99 "ND": FLAG 40
** (83) missing
count if onsetnumcod2b!=. & onsetnumcod2b!=999 & onsettxtcod2b==. //0
//list record_id ddda onsetnumcod2b onsettxtcod2b if onsetnumcod2b!=. & onsetnumcod2b!=999 & onsettxtcod2b==.
** (84) possibly invalid
count if onsetnumcod2b!=999 & onsettxtcod2b==99 //25
//list record_id ddda duration* onsetnumcod2b onsettxtcod2b if onsetnumcod2b!=999 & onsettxtcod2b==99
replace onsetnumcod2b=999 if onsetnumcod2b==. & onsettxtcod2b==99 //23 changes
//replace onsetnumcod2b=999 if onsetnumcod2b!=999 & onsettxtcod2b==99 //125 changes
//replace onsettxtcod2b=. if onsetnumcod2b!=999 & onsettxtcod2b==99 //125 changes
count if onsetnumcod2b==999 & onsettxtcod2b==99 //1
replace onsettxtcod2b=. if onsetnumcod2b==999 & onsettxtcod2b==99 //1 change


** pod: Text, if missing=99: FLAG 41
** (85) missing
count if event==1 & pod=="" //0
//list record_id ddda pod if event==1 & pod==""
** (86) invalid
count if event==1 & regexm(pod, "[a-z]") //0
//list record_id ddda pod if event==1 & regexm(pod, "[a-z]")
replace pod=upper(pod) //0 changes
replace pod = rtrim(ltrim(itrim(pod))) //0 changes
count if event==1 & regexm(pod,"NIL")|event==1 & regexm(pod,"ND") //1,836 - all correct
//list record_id ddda pod if event==1 & regexm(pod,"NIL")|event==1 & regexm(pod,"ND")


** deathparish: FLAG 42
** (87) missing
count if event==1 & deathparish==. //0
//list record_id ddda deathparish if event==1 & deathparish==.
** (88) invalid - see district demarcations noted below:
/*	Districts are assigned based on death parish
		District A - anything below top rock christ church and st. michael 
		District B - anything above top rock christ church and st. george
		District C - st. philip and st. john
		District D - st. thomas
		District E - st. james, st. peter, st. lucy
		District F - st. joseph, st. andrew
*/
** (88) Christ Church
count if deathparish==1 & district!=1 //2,159 - all correct
//list record_id ddda district pod deathparish if deathparish==1 & district!=1
** (89) St Andrew
count if deathparish==2 & district!=6 //0
//list record_id ddda deathparish district pod if deathparish==2 & district!=6
//replace district=6 if deathparish==2 & district!=6 //2 changes
** (90) St George
count if deathparish==3 & district!=2 //0
//list record_id ddda deathparish district pod if deathparish==3 & district!=2
//replace district=2 if deathparish==3 & district!=2 //1 change
** (91) St James
count if deathparish==4 & district!=5 //0
//list record_id ddda deathparish district pod if deathparish==4 & district!=5
//replace district=5 if deathparish==4 & district!=5 //1 change
** (92) St John
count if deathparish==5 & district!=3 //0
//list record_id ddda deathparish district pod if deathparish==5 & district!=3
//replace district=3 if record_id==2891 //1 change
** (93) St Joseph
count if deathparish==6 & district!=6 //0
//list record_id ddda deathparish district pod if deathparish==6 & district!=6
//replace district=6 if deathparish==6 & district!=6 //1 change
** (94) St Lucy
count if deathparish==7 & district!=5 //0
//list record_id ddda deathparish district pod if deathparish==7 & district!=5
//replace district=5 if record_id==2629 //1 change
** (95) St Michael
count if deathparish==8 & district!=1 //0
//list record_id ddda deathparish district pod if deathparish==8 & district!=1
//replace district=1 if deathparish==8 & district!=1 //2 changes
** (96) St Peter
count if deathparish==9 & district!=5 //0
//list record_id ddda deathparish district pod if deathparish==9 & district!=5
//replace deathparish=8 if record_id==185 //1 change
//replace district=5 if record_id==2568 // change
** (97) St Philip
count if deathparish==10 & district!=3 //0
//list record_id ddda deathparish district pod if deathparish==10 & district!=3
//replace deathparish=3 if deathparish==10 & district!=3 //1 change
** (98) St Thomas
count if deathparish==11 & district!=4 //0
//list record_id ddda deathparish district pod if deathparish==11 & district!=4
//replace district=4 if deathparish==11 & district!=4 //2 changes


** regdate: Y-M-D: FLAG 43
** (99) missing
count if event==1 & regdate==. //1 correct
//list record_id ddda pname dod regdate dodyear if event==1 & regdate==.

** (100) invalid - future date
count if event==1 & regdate!=. & regdate>today //0
sort record_id
//list record_id ddda dod regdate pname if event==1 & regdate!=. & regdate>today
//replace pname=subinstr(pname,"J","JO",.) if record_id==1789 //1 change
//replace regdate=regdate-365.25 if record_id==1789 //1 change
drop today


** certifier: Text, if missing=99: FLAG 44
** (101) missing
count if event==1 & certifier=="" //0
//list record_id ddda certifier if event==1 & certifier==""
count if certifier=="999" //0
//replace certifier="99" if certifier=="999" //1 change
** (102) invalid
count if event==1 & regexm(certifier, "[a-z]") //0
//list record_id ddda certifier if event==1 & regexm(certifier, "[a-z]")
replace certifier=upper(certifier) //0 changes
replace certifier = rtrim(ltrim(itrim(certifier))) //0 changes
count if event==1 & regexm(certifier,"NIL")|event==1 & regexm(certifier,"ND") //801 - all correct
//list record_id ddda certifier if event==1 & regexm(certifier,"NIL")|event==1 & regexm(certifier,"ND")


** certifieraddr: Text, if missing=99: FLAG 45
** (103) missing
count if event==1 & certifieraddr=="" //0
//list record_id ddda certifieraddr if event==1 & certifieraddr==""
replace certifieraddr="99" if event==1 & certifieraddr=="" //0 changes
** (104) invalid
count if event==1 & regexm(certifieraddr, "[a-z]") //0
//list record_id ddda certifieraddr if event==1 & regexm(certifieraddr, "[a-z]")
replace certifieraddr=upper(certifieraddr) //0 changes
replace certifieraddr = rtrim(ltrim(itrim(certifieraddr))) //1 change
count if event==1 & regexm(certifieraddr,"NIL")|event==1 & regexm(certifieraddr,"ND") //285 - all correct
//list record_id ddda certifieraddr if event==1 & regexm(certifieraddr,"NIL")|event==1 & regexm(certifieraddr,"ND")


** namematch: readonly: FLAG 46
** (105) missing
count if event==1 & namematch==. //2 - already checked for duplicates above; these are ones that were added into 2008-2020 db from 2018 and 2019 dbs post-cleaning
replace namematch=2 if event==1 & namematch==. //2 changes
//list record_id ddda namematch if event==1 & namematch==.


** cleaned: 1=Yes; 2=No: FLAG 47
** (105) missing
count if event==1 & cleaned==. //2
count if event==1 & cleaned==2 //0
//tab cleaned if event==1 ,m
replace cleaned=1 if event==1 & (cleaned==.|cleaned==2) //2 changes


** death_certificate_complete (auto-generated by REDCap): 0=Incomplete 1=Unverified 2=Complete: FLAG 48
** (106) missing
count if event==1 & recstatdc==. //0
** (107) invalid - incomplete and no reason given in field comment of redcapdb
count if event==1 & recstatdc==0 //0
//list record_id ddda dddoa recstatdc if event==1 & recstatdc==0
replace recstatdc=2 if event==1 & recstatdc==0 //0 changes
** (108) invalid - unverified and no reason given in field comment of redcapdb
count if event==1 & recstatdc==1 //0


*******************
** TRACKING FORM **
*******************

** tfdddoa: Y-M-D, readonly: FLAG 49
** (109) missing
count if event==2 & tfdddoa==. //0
//list record_id tfdddoaend if event==2 & tfdddoa==.
replace tfdddoa=tfdddoaend if event==2 & tfdddoa==. //0 changes


** tfdddoatstart: H:M, readonly: FLAG 50
** (110) missing
count if event==2 & tfdddoatstart==. //0


** tfddda: readonly, user logged into redcap: FLAG 51
** (111) missing
count if event==2 & tfddda==. //12 - checked Logging in REDCap db
//list record_id tfddda2 if event==2 & tfddda==.
replace tfddda=4 if tfddda2=="4" //0 changes
replace tfddda=13 if tfddda2=="13" //0 changes
replace tfddda=14 if tfddda2=="14" //0 changes
replace tfddda=20 if tfddda2=="20" //0 changes
replace tfddda=25 if tfddda2=="25" //0 changes
replace tfddda=98 if tfddda2=="98" //0 changes
replace tfddda=98 if event==2 & tfddda==. //12 changes
count if event==2 & tfddda==. //0
count if length(tfddda2)<4 & tfddda2!="" //0
//list record_id tfddda2 if length(tfddda2)<4 & tfddda2!=""
/*
replace tfddda2="ivanna.bascombe" if tfddda2=="ib" //1 change
replace tfddda2="asia.blackman" if tfddda2=="ab" //9 changes
replace tfddda2="karen.greene" if tfddda2=="kg"|tfddda2=="4" //18 changes
replace tfddda2="kirt.gill" if tfddda2=="kwg"|tfddda2=="13" //10 changes
replace tfddda2="tamisha.hunte" if tfddda2=="th"|tfddda2=="14" //6 changes
replace tfddda2="nicolette.roachford" if tfddda2=="nr"|tfddda2=="20" //6 changes
replace tfddda2="ashley.henry" if tfddda2=="ah"|tfddda2=="25" //25 changes
replace tfddda2="bnr.intern" if tfddda2=="98" //8 changes
replace tfddda2="thelema.grannum" if tfddda2=="tg"|tfddda2=="t.g"|tfddda2=="t.g." //4 changes
*/
count if tfddda!=. & tfddda2=="" //0
count if tfddda2!="" & tfddda==. //0


** tfregnumstart: integer: FLAG 52
** (112) missing
count if event==2 & tfregnumstart==. //0
//list record_id if event==2 & tfregnumstart==.
//drop if record_id==3035 //1 deleted - blank record


** tfdistrictstart: letters only: FLAG 53
** (113) missing
count if event==2 & tfdistrictstart=="" //0
** (114) invalid
count if event==2 & regexm(tfdistrictstart, "[a-z]") //0
//replace tfdistrictstart = rtrim(ltrim(itrim(tfdistrictstart))) //0 changes


** tfregnumend: integer: FLAG 54
** (115) missing
count if event==2 & tfregnumend==. //0
//list record_id tfddda tfregnumend if event==2 & tfregnumend==.


** tfdistrictend: letters only: FLAG 55
** (116) missing
count if event==2 & tfdistrictend=="" //0
//list record_id tfddda tfdistrictend if event==2 & tfdistrictend==""
** (117) invalid
count if event==2 & regexm(tfdistrictend, "[a-z]") //0
//replace tfdistrictend = rtrim(ltrim(itrim(tfdistrictend))) //0 changes


** tfdddoaend: Y-M-D, readonly: FLAG 56
** (118) missing
count if event==2 & tfdddoaend==. //0
//list record_id tfdddoa if event==2 & tfdddoaend==.
replace tfdddoaend=tfdddoa if event==2 & tfdddoaend==. //0 changes


** tfdddoatend: H:M, readonly: FLAG 57
** (119) missing
count if event==2 & tfdddoatend==. //129 - leave as blank; check TF regnumstart and regnumend Then all data report by that regnum in REDCap
//list record_id tfddda tfdddoatstart if event==2 & tfdddoatend==.
//replace tfdddoatend=tfdddoatstart if record_id==41
//replace tfdddoatstart= clock("31dec1899 08:58:00", "DMYhms") if record_id==41


** tfddelapsedh: auto-generated, readonly: FLAG 58
** (120) missing
count if tfdddoatstart!=. & tfdddoatend!=. & tfddelapsedh==. //0
//list record_id tfddda tfdddoatstart tfdddoatend if tfdddoatstart!=. & tfdddoatend!=. & tfddelapsedh==.
replace tfddelapsedh=hours(tfdddoatend-tfdddoatstart) if tfdddoatstart!=. & tfdddoatend!=. & tfddelapsedh==. //0 changes
format tfddelapsedh %9.0f


** tfddelapsedm: auto-generated, readonly: FLAG 59
** (120) missing
count if tfdddoatstart!=. & tfdddoatend!=. & tfddelapsedm==. //0
//list record_id tfddda tfdddoatstart tfdddoatend if tfdddoatstart!=. & tfdddoatend!=. & tfddelapsedm==.
replace tfddelapsedm=minutes(tfdddoatend-tfdddoatstart) if tfdddoatstart!=. & tfdddoatend!=. & tfddelapsedm==. //0 changes


** tfddtxt: FLAG 60
replace tfddtxt = rtrim(ltrim(itrim(tfddtxt))) //0 changes


** tracking_complete (auto-generated by REDCap): 0=Incomplete 1=Unverified 2=Complete: FLAG 61
** (121) missing
count if event==2 & recstattf==. //0
** (122) invalid - incomplete and no reason given in field comment of redcapdb
count if event==2 & recstattf==0 //0
//list record_id tfddda tfdddoa recstattf if event==2 & recstattf==0
replace recstattf=2 if event==2 & recstattf==0 //1 change
** (123) invalid - unverified and no reason given in field comment of redcapdb
count if event==2 & recstattf==1 //0


** REMOVE dod>2020
drop if event==1 & dodyear>2020 //0 deleted - already deleted above

** ORDER variables according to position in DeathData REDCap database
order record_id redcap_event_name event dddoa ddda odda certtype regnum district pname address parish ///
	  sex age agetxt nrnnd nrn mstatus occu durationnum durationtxt dod dodyear ///
	  cod1a onsetnumcod1a onsettxtcod1a cod1b onsetnumcod1b onsettxtcod1b ///
	  cod1c onsetnumcod1c onsettxtcod1c cod1d onsetnumcod1d onsettxtcod1d ///
	  cod2a onsetnumcod2a onsettxtcod2a cod2b onsetnumcod2b onsettxtcod2b ///
	  pod deathparish regdate certifier certifieraddr namematch cleaned recstatdc ///
	  tfdddoa tfdddoatstart tfddda tfregnumstart tfdistrictstart tfregnumend tfdistrictend ///
      tfddelapsedh tfddelapsedm tfdddoaend tfdddoatend tfddtxt recstattf

count //32,465

label data "BNR MORTALITY data 2008-2020"
notes _dta :These data prepared from BB national death register & BNR (Redcap) deathdata database
save "`datapath'\version05\3-output\2008-2020_deaths_cleaned_dqi_dc" ,replace


** REMOVE variables and labels not needed in DeathData REDCap database
drop birthdate record_id2 flag* corr_* birthdate birthdate2
label drop _all

** REDCap will not import H:M:S format so had to change cfdate from %tcCCYY-NN-DD_HH:MM:SS to below format
format dddoa %tcCCYY-NN-DD_HH:MM

***************
** FORMATTING  
***************
drop event
gen str4 regnum2 = string(regnum,"%04.0f")
drop regnum
rename regnum2 regnum
replace regnum="" if regnum=="." //212 changes
rename recstatdc death_certificate_complete
drop tfddda
rename tfddda2 tfddda
rename recstattf tracking_complete

order record_id	redcap_event_name dddoa	ddda odda certtype regnum district pname address ///
	  parish sex age agetxt nrnnd nrn mstatus occu durationnum durationtxt dod dodyear ///
	  cod1a onsetnumcod1a onsettxtcod1a cod1b onsetnumcod1b onsettxtcod1b ///
	  cod1c onsetnumcod1c onsettxtcod1c cod1d onsetnumcod1d onsettxtcod1d ///
	  cod2a onsetnumcod2a onsettxtcod2a cod2b onsetnumcod2b onsettxtcod2b pod deathparish ///
	  regdate certifier certifieraddr namematch duprec cleaned elecmatch death_certificate_complete ///
	  tfdddoa tfddda tfregnumstart tfdistrictstart tfregnumend tfdistrictend tfddtxt tracking_complete

	  
count //32,465

label data "BNR MORTALITY data 2008-2020"
notes _dta :These data prepared from BB national death register & BNR (Redcap) deathdata database
save "`datapath'\version05\3-output\2008-2020_deaths_cleaned_export_dc" ,replace

